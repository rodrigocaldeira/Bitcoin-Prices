HAI 1.3

  OBTW
    GETS THE BITCOIN PRICE OF MARKET AND CURRENCY

    ALL THE "BTW" IN THE CODE ARE COMMENT, LIKE THE "//" FROM C/C++, PHP, JS, ETC...

    
  TLDR

  BTW INCLUDES...
  CAN HAS STDIO?
  CAN HAS STRING?
  CAN HAS SOCKS?

  I HAS A MARKET ITZ A YARN
  I HAS A MONEY ITZ A YARN

  BTW GETS THE MARKET
  VISIBLE "WAT MARKET U WANT? "!
  GIMMEH MARKET

  BTW GET THE CURRENCY
  VISIBLE "GIBE MONI PLOX: "!
  GIMMEH MONEY

  BTW URL :)
  I HAS A LINK ITZ SMOOSH "http://api.bitcoincharts.com/v1/trades.csv?symbol=" AN MARKET AN MONEY MKAY 

  BTW SERVER ADDRESS
  I HAS A SERVER ITZ "bitcoincharts.com"

  BTW CREATE A CONNECTION
  I HAS A LOCAL ITZ I IZ SOCKS'Z BIND YR "ANY" AN YR 12345 MKAY

  BTW RESOLVE THE ADDRESS
  I HAS A ADDR ITZ I IZ SOCKS'Z RESOLV YR SERVER MKAY

  BTW OPENS A CONNECTION TO THE SERVER
  I HAS A REMOTE ITZ I IZ SOCKS'Z KONN YR LOCAL AN YR ADDR AN YR 80 MKAY

  BTW SEND A HTTP REQUEST (METHOD=GET, PORT=80)
  I HAS A REQ ITZ SMOOSH "GET " AN LINK AN ":)" MKAY
  I IZ SOCKS'Z PUT YR LOCAL AN YR REMOTE AN YR REQ MKAY

  BTW READS THE RESULT
  I HAS A DATA ITZ I IZ SOCKS'Z GET YR LOCAL AN YR REMOTE AN YR 500 MKAY

  BTW CLOSE THE CONNECTION
  I IZ SOCKS'Z CLOSE YR LOCAL MKAY

  OBTW
    LOLCODE DOESN'T HAVE ANY SUBSTRING METHOD (FUNCTION, WAHTEVER.....)
    SO WE NEED TO FIND THE PRICE KNOWING WHERE IT IS LOCATED.
    IN THAT CASE, ITS BETWEEN THE FIRST TWO COMMAS
  TLDR

  I HAS A DATA_LEN ITZ I IZ STRING'Z LEN YR DATA MKAY BTW DATA'S LENGTH
  I HAS A COMMA ITZ FAIL BTW FLAG FOR THE FIRST COMMA WE FIND
  I HAS A BTC_VALEU ITZ A YARN BTW THE BITCOIN VALUE
  I HAS A BTCOK ITZ FAIL BTW FLAG FOR THE SECOND (AND LAST) COMMA WE NEED


  BTW LOOP THROUGH THE DATA, A CHAR AT TIME
  IM IN YR LOOP UPPIN YR I TIL BOTH SAEM I AN DATA_LEN

    BTW THE CHAR AT 'I'
    I HAS A CAHR ITZ I IZ STRING'Z AT YR DATA AN YR I MKAY
    
    BTW SWITCH OF LOLCODE, LOL!
    CAHR, WTF?
    OMG "," BTW WE FOUND A COMMA
      NOT COMMA, O RLY? BTW IS THIS OUR FISRT COMMA?
        YA RLY, COMMA R WIN BTW YEP!
        NO WAI BTW NOPS! ITS OUR SECOND COMMA. WE FOUND THE BTC VALUE!
          BTCOK R WIN
          GTFO
      OIC
    OMGWTF BTW THE "DEFAULT" OF THE SWITCH (LOL AGAIN)
      COMMA, O RLY? BTW HAVE WE ALREADY FOUND OUR FIRST COMMA?
        YA RLY, BTC_VALEU R SMOOSH BTC_VALEU AN CAHR MKAY BTW YEP! CONCATENATE THE CHAR TO THE BTC VALUE!
      OIC
    OIC

    BTW IF THE FOUND THE VALUE OF BITCOIN, WE GET OUT OF OUR LOOP
    BTCOK, O RLY?, YA RLY, GTFO, OIC BTW "GTFO" IS THE "BREAK" FOR LOLCODE (LOLOLOLOLOLOL)
  IM OUTTA YR LOOP BTW THE END OF THE LOOP

  BOTH SAEM BTC_VALEU AN "", O RLY? BTW IF (BTC_VALEU == "")
    YA RLY, VISIBLE "sRY, I DUNNO WHAT U WANT" BTW YES. BTC VALUE NOT FOUND
    NO WAI, VISIBLE SMOOSH "OH, HAI! THE BITCOIN VALUE NOW IZ: " AN BTC_VALEU MKAY BTW NOPE! SHOW THE VALUE!
  OIC 
KTHXBYE